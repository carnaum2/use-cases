USE VF_EBU;

set mapred.job.name = WORK_CARTERA_TEMP;
DROP TABLE WORK.WORK_CARTERA_TEMP;
CREATE TABLE WORK.WORK_CARTERA_TEMP AS
SELECT  CIF_NIF,
UPPER(ASIGNACION_COMERCIAL) AS ASIGNACION_COMERCIAL,
VF_FUNC.TO_DATE(CONCAT(SUBSTR('${hiveconf:MONTH0}',1,4),'-',SUBSTR('${hiveconf:MONTH0}',5,2),'-01')) AS MONTH_DATE,
VF_FUNC.TO_DATE(CONCAT(SUBSTR(PROMOCION_FECHAFIN,1,4),'-',SUBSTR(PROMOCION_FECHAFIN,5,2),'-01')) AS PROMOCION_FECHAFIN
FROM 
INPUT.VF_EBU_AC_EMP_CARTERA_CLIENTE WHERE PARTITIONED_MONTH = '${hiveconf:MONTH0}';


set mapred.job.name = WORK_PERMANENCIA_TEMP;
DROP TABLE WORK.WORK_PERMANENCIA_TEMP;
CREATE TABLE WORK.WORK_PERMANENCIA_TEMP AS
SELECT  CIF_NIF, 
MSISDN,
VF_FUNC.TO_DATE(CONCAT(SUBSTR(PROMOCION_DATOS_FECHAFIN,1,4),'-',SUBSTR(PROMOCION_DATOS_FECHAFIN,5,2),'-01')) AS PROMOCION_DATOS_FECHAFIN,
VF_FUNC.TO_DATE(CONCAT(SUBSTR(PROMOCION_VOZ_FECHAFIN,1,4),'-',SUBSTR(PROMOCION_VOZ_FECHAFIN,5,2),'-01')) AS PROMOCION_VOZ_FECHAFIN,
VF_FUNC.TO_DATE(CONCAT(SUBSTR(PROMOCION_VOZ_PP_FECHAFIN,1,4),'-',SUBSTR(PROMOCION_VOZ_PP_FECHAFIN,5,2),'-01')) AS PROMOCION_VOZ_PP_FECHAFIN
FROM
INPUT.VF_EBU_AC_EMP_PERMANENCIAS_SERVICIO WHERE PARTITIONED_MONTH = '${hiveconf:MONTH0}';


set mapred.job.name = WORK_PERMANENCIA;
DROP TABLE WORK.WORK_PERMANENCIA;
CREATE TABLE WORK.WORK_PERMANENCIA AS
SELECT
A.*,
GREATEST (NVL(NUM_PERMANENCIA_DATOS,-99999),
NVL(NUM_PERMANENCIA_VOZ,-99999),
NVL(NUM_PERMANENCIA_PP_VOZ,-99999)) AS NUM_PERMANENCIA_GLOBAL,
NVL(NUM_PERMANENCIA_VOZ,-99999) AS NUM_PERMANENCIA_LINEA
FROM(
SELECT
A.CIF_NIF AS NIF,
A.ASIGNACION_COMERCIAL,
B.MSISDN,
FLOOR(VF_FUNC.MONTHS_BETWEEN(A.PROMOCION_FECHAFIN,A.MONTH_DATE))AS NUM_PERMANENCIA_CLI,
FLOOR(VF_FUNC.MONTHS_BETWEEN(B.PROMOCION_DATOS_FECHAFIN,A.MONTH_DATE))AS NUM_PERMANENCIA_DATOS,
FLOOR(VF_FUNC.MONTHS_BETWEEN(B.PROMOCION_VOZ_FECHAFIN,A.MONTH_DATE)) AS NUM_PERMANENCIA_VOZ,
FLOOR(VF_FUNC.MONTHS_BETWEEN(B.PROMOCION_VOZ_PP_FECHAFIN,A.MONTH_DATE)) AS NUM_PERMANENCIA_PP_VOZ
FROM WORK.WORK_CARTERA_TEMP A
LEFT JOIN
WORK.WORK_PERMANENCIA_TEMP B
ON A.CIF_NIF = B.CIF_NIF) A;


set mapred.job.name = EMP_EXP_PERMA_CLI;
DROP TABLE VF_EBU.EMP_EXP_PERMA_CLI;
CREATE TABLE VF_EBU.EMP_EXP_PERMA_CLI AS
SELECT
NIF,
ASIGNACION_COMERCIAL,
CASE WHEN CAST(NUM_PERMANENCIA_CLI AS DOUBLE) > 0 THEN 1
WHEN PCT_LINEAS_BLINDA>= 0.5 AND ASIGNACION_COMERCIAL IN ('DISTRIBUCION', 'SACO DISTRIBUCION') THEN 1
WHEN PCT_LINEAS_BLINDA>= 0.6 AND ASIGNACION_COMERCIAL IN ('TELEVENTA') THEN 1 ELSE 0 END AS IND_CLI_BLINDADO,
CASE WHEN CAST(NUM_PERMANENCIA_CLI AS DOUBLE) > 0 THEN NUM_PERMANENCIA_CLI ELSE NUM_PERMANENCIA_LINEA_MAX END AS NUM_PERMANENCIA_CLI,
'${hiveconf:MONTH0}' AS MES
FROM (
SELECT
NIF,
MAX(ASIGNACION_COMERCIAL) AS ASIGNACION_COMERCIAL,
MAX(NVL(NUM_PERMANENCIA_CLI,-99999)) AS NUM_PERMANENCIA_CLI,
MAX(NVL(NUM_PERMANENCIA_LINEA,-99999)) AS NUM_PERMANENCIA_LINEA_MAX,
SUM(CASE WHEN CAST(NUM_PERMANENCIA_LINEA AS DOUBLE) > 0 AND NUM_PERMANENCIA_LINEA IS NOT NULL THEN 1 END) AS NUM_LINEAS_BLINDA,
COUNT(*) AS NUM_LINEAS,
SUM(CASE WHEN CAST(NUM_PERMANENCIA_LINEA AS DOUBLE) > 0 AND NUM_PERMANENCIA_LINEA IS NOT NULL THEN 1 END) / COUNT(*) AS PCT_LINEAS_BLINDA
FROM WORK.WORK_PERMANENCIA
GROUP BY NIF)A;  


DROP TABLE WORK.WORK_CARTERA_TEMP;
DROP TABLE WORK.WORK_PERMANENCIA_TEMP;
DROP TABLE WORK.WORK_PERMANENCIA;

EXIT;
